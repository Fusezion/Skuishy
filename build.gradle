plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'lol.aabss'
version = '2.1'

repositories {
    mavenCentral()
    // paper
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    // skipt
    maven {
        url = 'https://repo.skriptlang.org/releases'
    }
    // jitpack
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    // paper
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
    implementation "net.kyori:adventure-text-minimessage:4.15.0"
    // Skript
    compileOnly(group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0') {
        transitive = false
    }
    // decent holograms
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.8.6'
    // vivecraft
    implementation files('libs/Vivecraft_Spigot_Extensions.jar')
    // vulcan
    implementation files('libs/VulcanAPI.jar')
    // other things
    implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.800')
    implementation 'org.json:json:20231013'
}

shadowJar {
    dependencies {
        include(dependency('org.json:json:20231013'))
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    from ('lang') {
        include '**/*'
        into 'lang/'
    }
}
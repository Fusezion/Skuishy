plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "lol.aabss"
version = "2.7"

repositories {
    mavenCentral()
    mavenLocal()
    maven {url = "https://repo.papermc.io/repository/maven-public/"}
    maven {url = "https://repo.skriptlang.org/releases"}
    maven {url = "https://jitpack.io"}
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-text-minimessage:4.15.0"
    compileOnly ("com.github.SkriptLang:Skript:2.7.0") {
        transitive = false
    }
    compileOnly "com.github.decentsoftware-eu:decentholograms:2.8.8"
    compileOnly files("libs/VulcanAPI.jar")
    implementation "org.jsoup:jsoup:1.17.2"
    implementation "com.github.SkriptLang:skript-reflect:2.4-dev1"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

shadowJar {
    dependencies {
        include(dependency("org.jsoup:jsoup:1.17.2"))
    }
}

def oldestJava = 17
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
    from ("lang") {
        include "**/*"
        into "lang/"
    }
}